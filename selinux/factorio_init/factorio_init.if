
## <summary>policy for factorio_init/summary>

########################################
## <summary>
##	Execute factorio_init_exec_t in the factorio domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`factorio_init_domtrans',`
	gen_require(`
		type factorio_init_t, factorio_init_exec_t;
	')

	corecmd_search_bin($1);
	domtrans_pattern($1, factorio_init_exec_t, factorio_init_t);
	allow factorio_init_t $1:process { signal }; # Allow misc signals to be sent up.
	auditallow $1 factorio_init_exec_t:file { entrypoint };
	auditallow $1 factorio_init_t:process { transition };
')

######################################
## <summary>
##	Execute factorio_init in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`factorio_init_exec',`
	gen_require(`
		type factorio_init_exec_t;
	')

	corecmd_search_bin($1);
	can_exec($1, factorio_init_exec_t);
')

######################################
## <summary>
##	Manage all files and directories in the factorio domain
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`factorio_init_manage_file_dirs',`
	gen_require(`
		type factorio_init_t, factorio_init_exec_t, factorio_init_conf_t;
	')
	
	allow $1 factorio_init_t:dir manage_dir_perms;
	allow $1 factorio_init_t:file manage_file_perms;
	allow $1 factorio_init_exec_t:file manage_file_perms;
	allow $1 factorio_init_conf_t:file manage_file_perms;
')

######################################
## <summary>
##	Relabel all files and directories in the factorio domain
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`factorio_init_relabel_file_dirs',`
	gen_require(`
		type factorio_init_t, factorio_init_exec_t, factorio_init_conf_t;
	')
	
	allow $1 factorio_init_t:dir { relabelto relabelfrom };
	allow $1 factorio_init_t:file { relabelto relabelfrom };
	allow $1 factorio_init_exec_t:file { relabelto relabelfrom };
	allow $1 factorio_init_conf_t:file { relabelto relabelfrom };
	allow $1 factorio_init_t:lnk_file { relabelto relabelfrom };
')
